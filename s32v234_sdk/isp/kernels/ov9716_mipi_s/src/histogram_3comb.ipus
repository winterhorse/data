// for IPUS with Histogram engine
// 0 channels in: bayer-pattern L,  starting with B
// 1 channels in: bayer-pattern S,  starting with B
// 2 channels in: bayer-pattern VS, starting with B
// 1 channel out: na
// histogram out:
//   0-127: L   histogram (128 bins)
// 128-191: S   histogram (64 bins)
// 102-155: VS  histogram (64 bins)
// parameters:
// gpr0        line length (active pixels)
// gpr1        gap between lines (in multi trigger)
// gpr2        brightness control (12.4b, 4096 neutral, higher value makes darker)
// used registers:
// gpr4        bin offset between L ald VS = 192
// gpr5        shift saturated L value

.global histogram_3comb_start
histogram_3comb_start:

  mov  confalu,(0 /* unsigned*/ | (1 << 1) /* saturate*/ | (12<<4) /* shr =8*/ )

  mov confhist,(((16-6)<<8) | 128)// for S
                                //   shift right for 16bits -> 6bits
                                //   offset from 128 bins
  mov gpr4,192                  //   offset between L and VS in the hist
  mov lock,(1<<9)               // unlock histogram read out
  done histogram_3comb_linestart,i   // load first pixels

histogram_3comb_linestart:
  mov loopcnt,gpr0              // set active line length for multi trigger

histogram_3comb_loop:
  lsr gpr5,ina0,(16-6)          // shift V from 16bit > 6 bits
  add hbinincl,gpr5,gpr4        // add to histogram with offset 192
  			      
  asl hbininch,ina0,4           // saturate combined at bit 12 for S and
                                //   add to histogram
  asl gpr5,ina0,8               // saturate combined at bit 8 for L
  lsr hbinincl,gpr5,(16-7)      // shift saturated L from 16 to 7 bits
  ldot ina0,histogram_3comb_loop,ixo // next pixels (copy nput to output)

  // skip gap between
  mov loopcnt,gpr1              // set gap between lines in multi trigger
histogram_3comb_linegap:
  ldon histogram_3comb_linegap,i // skip pixels in gap from input
  bal histogram_3comb_linestart

// halt
.global histogram_3comb_end
histogram_3comb_end:
