# IPUS
# Function:
# compute low pass filtered bysum of 8 neighbors
# if absolute difference between low pass filtered and input is greater
# than threshold, then use input, else use low pass filtered
# Channels:
# set shift_in/x/out to 1 pixels
# in0  :  Y
# in1  :  Y
# in2  :  Y
# out0 :  Y denoised / or original

# box coefficients
# 1  1  1                   3 3 3
# 1  0  1   right shift = > 3 - 3
# 1  1  1                   3 3 3

# sum = 8

# Parameters:
# GPR7       threshold
# GPR12      0: mark edge pixels black 0xffff: normal mode.
# GPR13      maximum ypos
# Variables:
# GPR8       absdiff(low_pass_filtered - input)
# GPR10      low pass filtered signal
# GPR14      # of pixels above threshold of current frame
# GPR15      # of pixels above threshold of last frame

.global denoise_box_start
denoise_box_start:
        sub zero,ypos,zero     // check for frame start
	beq denoise_box_framestart  // init counter

denoise_box_continue:
        mov confalu,(0 /*unsigned*/ | (1<<1) /*saturate*/ | (4<<4) /*shr*/)
        mov confaddt,(0 /*w*/ | (0<<2) /*use mask*/ | (0<<3) /*unsigned*/)
	max gpr13,gpr13,ypos

	mov maskv, 0b111101111 // all except center
	mov w,3
	swp                    // mov shift mask to ww

        done denoise_box_d1,i
denoise_box_d1:
        done denoise_box_d2,i
denoise_box_d2:
        done denoise_box_loop,i
denoise_box_loop:

        // compare difference with threshold
	//and gpr9,ina4,gpr12  // to view edge pixels
	lsr w,ina,ww
	// nop // stall one cycle from updating w to getting the su
	mov gpr10,sum
        abd gpr8,gpr10,ina4

	done denoise_box_check_thr,i  // to reduce stalls
denoise_box_check_thr:
        sub gpr8,gpr8,gpr7
	// nop  //stall before branch

	// select filtered if lower than threshold
	and gpr9,ina5,gpr12  // to view edge pixels
        bcc denoise_box_above_thr    // carry cleared
        dout gpr10,denoise_box_loop,xo

denoise_box_above_thr:
	// keep input signal when higher than threshold
	add gpr14,1
	dout gpr9,denoise_box_loop,xo
	//dout ina5,denoise_box_loop,xo

 denoise_box_framestart:
	mov gpr15,gpr14
	mov gpr14,zero
	bal denoise_box_continue

       //halt
.global denoise_box_end
denoise_box_end:
