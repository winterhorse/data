# IPUS 
//gpr0		R black  offset: default 0xf08 
//gpr1		Gr black offset: default 0xf08
//gpr2		Gb black offset: default 0xf08
//gpr3		B black  offset: default 0xf08
//
//gain factors
//gpr4		R  gain default: 400 (Soyn cam), 590 (Sony EVB), 
//gpr7		B  gain	default: 490 (Soyn cam), 402 (Sony EVB), 
//
//knee point from 0 - 256
//gpr8		factor for line 0 (long exposure)
//gpr9		factor for line 1 (short) computed from gpr8 and gpr9

.global exposure_tonehdr2x_start
exposure_tonehdr2x_start:

        mov  confalu,(0 /* unsigned*/ | (1 << 1) /* saturate*/ | (8<<4) /* shr =8*/ ) 
        //mov confshr,8
	mov gpr9,256
	sub gpr9,gpr9,gpr8

  
        done exposure_tonehdr2x_d1_2,i
exposure_tonehdr2x_d1_2:
        lsl zero,ypos,15
        mov lock,0
        bne exposure_tonehdr2x_oddline1


  
exposure_tonehdr2x_evenline1:
        mov confhist,((10 <<8) | 64)
        mov gpr12,10             # ina(15:0)  >> 10 for histogram  hbinl
        mov gpr13,0
exposure_tonehdr2x_loop0_0:  //R
        sub ina0,ina0,gpr0       # black correction
        mulh ina0,ina0,gpr4      # channel gain
        sub ina3,ina3,gpr0       # black correction
        mulh ina3,ina3,gpr4      # channel gain

        mulh sacc0,ina0,gpr8     # hdr2x knee point
        mulh acc0,ina3,gpr9      # hdr2x knee2

        lsr hbinincl,ina0,gpr12  # long exposure histogram (0-63)
        dout acc0,exposure_tonehdr2x_loop0_1,ixo

exposure_tonehdr2x_loop0_1:  //GR
        sub ina0,ina0,gpr1       # black correction
        //mulh ina0,ina0,gpr5      # channel gain
        sub ina3,ina3,gpr1       # black correction
        //mulh ina3,ina3,gpr5      # channel gain

        mulh sacc0,ina0,gpr8     # hdr2x knee point
        mulh acc0,ina3,gpr9      # hdr2x knee2

        mov hbininch,ina0        # long exposure histogram
        dout acc0,exposure_tonehdr2x_loop0_0,ixo

exposure_tonehdr2x_oddline1:
        mov confhist,((10 <<8) | 192)
        mov gpr12,10             # ina(15:0)  >> 10 for histogram  hbinincl (64 bins per component)
        mov gpr13,128            # offset bin for odd lines

exposure_tonehdr2x_loop1_0:  // GB
        sub ina0,ina0,gpr2       # black correction
        //mulh ina0,ina0,gpr6      # channel gain
        sub ina3,ina3,gpr2       # black correction
        //mulh ina3,ina3,gpr6      # channel gain

        mulh sacc0,ina0,gpr8     # hdr2x knee point
        mulh acc0,ina3,gpr9      # hdr2x knee2

        lsr ina0,ina0,gpr12      # scale for 64 bins
        add hbinincl,ina0,gpr13  # long exposure histogram
        dout acc0,exposure_tonehdr2x_loop1_1,ixo

exposure_tonehdr2x_loop1_1: //B
        sub ina0,ina0,gpr3       # black correction
        mulh ina0,ina0,gpr7      # channel gain
        sub ina3,ina3,gpr3       # black correction
        mulh ina3,ina3,gpr7      # channel gain

        mulh sacc0,ina0,gpr8     # hdr2x knee point
        mulh acc0,ina3,gpr9      # hdr2x knee2

        mov hbininch,ina0        # long exposure histogram
        dout acc0,exposure_tonehdr2x_loop1_0,ixo
  
//        halt
.global exposure_tonehdr2x_end
exposure_tonehdr2x_end:
